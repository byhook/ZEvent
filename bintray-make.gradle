apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'


/**
 * 读取配置
 */
def Properties mPublishProps = getPublishProperty();

//版本号
version = mPublishProps["VERSION"]

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    //资源前缀
    resourcePrefix "library_"

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
    }

    android {
        lintOptions {
            abortOnError false
        }
    }
}

dependencies {

}

/**
 * 获取构建版本
 * @return
 */
def getPublishProperty() {
    def Properties buildProps = new Properties()
    buildProps.load(new FileInputStream(file('make.properties')))
    return buildProps;
}

//项目的主页
def web_site = mPublishProps["WEB_SITE"]
//Git仓库主页
def git_site = mPublishProps["GIT_SITE"]
//Group ID
group = mPublishProps["GROUP_ID"]
install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                artifactId mPublishProps["ARTIFACT_ID"]
                name mPublishProps["LIB_NAME"]
                url web_site
                //设置授权信息
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                //填写的一些基本信息
                developers {
                    developer {
                        id 'byhook'
                        name 'Andy'
                        email 'byhook@163.com'
                    }
                }
                scm {
                    connection git_site
                    developerConnection git_site
                    url web_site
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        //发布的项目名称
        name = mPublishProps["LIB_NAME"]
        websiteUrl = web_site
        vcsUrl = git_site
        licenses = ["Apache-2.0"]
        publish = true
    }
}